plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.5.10'
  id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.10'
  id 'org.jetbrains.kotlin.plugin.jpa' version '1.5.10'

  id 'com.github.ben-manes.versions' version '0.39.0' // run to check latest versions ./gradlew dependencyUpdates
  id 'com.adarshr.test-logger' version '3.0.0'
  id 'jacoco'
}

allprojects {
  apply plugin: 'org.jetbrains.kotlin.jvm'
  apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
  apply plugin: 'org.jetbrains.kotlin.plugin.jpa'

  apply plugin: 'com.adarshr.test-logger'
  apply plugin: 'jacoco'
  
  group 'net.chala-sdk'
  version '0.1.0'
  
  repositories {
    mavenCentral()
  }

  dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
  }
  
  test {
    dependsOn cleanTest
    finalizedBy jacocoTestReport
  }

  jacoco {
    toolVersion = '0.8.7'
    reportsDirectory = file("${project.projectDir}/jacoco")
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name,
        'Implementation-Version': archiveVersion,
        'Built-By': 'micaelpedrosa@gmail.com'
    }
  }

  clean {
    delete "${project.projectDir}/jacoco"
  }
}